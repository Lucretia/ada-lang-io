---
sidebar_position: 3
description: How to start an SDLAda project.
keywords:
  - UTF-8
  - Unicode
  - GNAT
  - SDLAda
  - SDL2
---

# Starting an SDL2 project with SDLAda

The source code is in flux at the moment and there is an SDL specific game jam
coming up in August that I would like Ada people to enter to show what a good
language Ada is for game development.

**N.B**: In the following tutorial, use your own _game name_ where **`ada_game`**
is shown, use underscores to separate the words within the name.

This quite a long tutorial, but if we weren't using SDL2, it would be even longer.

In this tutorial I will show you how to set up a basic SDLAda application ready
for using the SDL2 2D renderer. Someone has been mad enough to create a [Playstation
1](https://github.com/PaoloMazzon/sdl3d) style 3D renderer using this API. To
see what others have made using it, keep refreshing the [SDL](https://www.libsdl.org)
main page under the "Made with SDL" sections.

As can be seen, a lot can be done with just this [API](https://wiki.libsdl.org/SDL2/CategoryRender) alone.

In the following code blocks, parts of the code may be highlighted, this is to
show the difference between input (highlighted) and output in the console, and
also to show context in source code where the snippets need to be placed if you
are following this.

Create a directory or find a suitable directory for you game development source and
use [Alire](https://ada-lang.io) to create a new project:

```console
// highlight-start
$ mkdir sdl2-tut
$ cd sdl2-tut
$ alr init --bin ada_game
// highlight-end
Enter a short description of the crate: (default: '')
> Example of SDLAda.
Select a software license for the crate?
  1. MIT OR Apache-2.0 WITH LLVM-exception
  2. MIT
  3. Apache-2.0 WITH LLVM-exception
  4. Apache-2.0
  5. BSD-3-Clause
  6. LGPL-3.0-or-later
  7. GPL-3.0-or-later WITH GCC-exception-3.1
  8. GPL-3.0-or-later
  9. Other...
Enter your choice index (first is default):
> 9
Enter SPDX license expression (https://spdx.org/licenses/): (default: '')
> Zlib
Enter a comma (',') separated list of tags to help people find your crate: (default: '')
> ada,gamedev,sdl2
Enter an optional Website URL for the crate: (default: '')
>
Using default: ''
‚úì ada_game initialized successfully.
```

Now, we want to use the SDL packages, but as they're in flux, it's best to use the
github HEAD sources, we do that by passing the `--use` flag with the URL of the
repositories.

```console
// highlight-next-line
$ alr with sdlada --use=https://github.com/ada-game-framework/sdlada.git
Cloning into '/<path to>/ada_game/alire/tmp/alr-ifht.tmp'...
remote: Enumerating objects: 1245, done.
remote: Counting objects: 100% (1245/1245), done.
remote: Compressing objects: 100% (603/603), done.
remote: Total 1245 (delta 943), reused 792 (delta 589), pack-reused 0
Receiving objects: 100% (1245/1245), 456.60 KiB | 5.93 MiB/s, done.
Resolving deltas: 100% (943/943), done.
‚ìò Pulling sdlada branch ...
Already up to date.
warn: Spent 0.14 seconds exploring complete solutions
warn: Exploring incomplete solutions to dependencies, this may take some time...
warn: The following external dependencies are unavailable within Alire:
warn:    libsdl2^2.0.0
warn: They should be made available in the environment by the user.
warn: Generating possibly incomplete configuration because of missing dependencies
‚ó∂ Syncing build dir... warn: Generating possibly incomplete environment because of missing dependencies
Requested changes:

   ‚úì sdlada ^2.5.73 (add)
warn: Spent 0.00 seconds exploring complete solutions
warn: Exploring incomplete solutions to dependencies, this may take some time...

Changes to dependency solution:

   New solution is incomplete.
   +üì¶      make    4.4.1  (new,indirect,executable in path)
   +üìå      sdlada  2.5.73 (new,path=alire/cache/pins/sdlada,url=https://github.com/ada-game-framework/sdlada.git)
   Missing:
   +‚ùó      libsdl2 ^2.0.0 (new,indirect,missing)

Do you want to proceed?
[Y] Yes  [N] No  (default is No) Y
```

Now, do the same with the other repos:

```console
$ alr with sdlada_mixer --use=https://github.com/ada-game-framework/sdlada_mixer.git
$ alr with sdlada_image --use=https://github.com/ada-game-framework/sdlada_image.git
$ alr with sdlada_ttf --use=https://github.com/ada-game-framework/sdlada_ttf.git
```

To test to make sure we can get autocompletion with the Ada language server, or
whatever you are using, we need to check alr's environment:

```console
// highlight-next-line
$ alr printenv | grep GPR_PRO
export GPR_PROJECT_PATH="/<path to>/ada_game:/<path to>/ada_game/alire/cache/pins/sdlada/build/gnat:/<path to>/ada_game/alire/cache/pins/sdlada_mixer:/<path to>/ada_game/alire/cache/pins/sdlada_image:/<path to>/ada_game/alire/cache/pins/sdlada_ttf:"
```

Now, edit the source:

```console
$ cd ada_game
$ TODO: Add command to set the editor.
$ alr edit
```

**N.B:** To get the Ada LSP server to do autocomplete with VSCode, I have always
needed to add the GPR to the workspace field in the VSCode settings.

You might want to reorganise the `depends-on` and the `pins` in the `toml.alire`
file:

```toml
...
executables = ["ada_game"]

[[depends-on]]
  sdlada       = "*"
  sdlada_mixer = "*"
  sdlada_image = "*"
  sdlada_ttf   = "*"

[[pins]]
  sdlada       = { url = "git@github.com:ada-game-framework/sdlada.git" }
  sdlada_mixer = { url = "git@github.com:ada-game-framework/sdlada_mixer.git" }
  sdlada_image = { url = "git@github.com:ada-game-framework/sdlada_image.git" }
  sdlada_ttf   = { url = "git@github.com:ada-game-framework/sdlada_ttf.git" }
```

Now change the `ada_game.gpr` file, such that the `Compiler` section looks like
this:

```ada
   package Compiler is
      for Default_Switches ("Ada") use Ada_Game_Config.Ada_Compiler_Switches &
         ("-gnat2022", "-gnatyM120");
   end Compiler;
```

Now do a test build:

```ada
$ alr build
```

It should checkout the subprojects into `alire/cache/pins` and build successfully.

## Creating a hardware accelerated window

Open the `src/ada_game.adb` source file in your editor. The first thing we want
to do is to open a window for the game to be displayed within, edit the file such
that it looks like the following:

```ada
with Ada.Strings.UTF_Encoding.Wide_Wide_Strings;
with SDL.Video.Windows.Makers;

procedure Ada_Game is
   package Encoders renames Ada.Strings.UTF_Encoding.Wide_Wide_Strings;
   package Video renames SDL.Video;
   package Windows renames Video.Windows;

   Game_Window : Windows.Window;
   Window_Size : constant SDL.Positive_Sizes := (640, 480);
begin
   if SDL.Initialise then
      Windows.Makers.Create
        (Win      => Game_Window,
         Title    => Encoders.Encode ("Ada SDL2 Tutorial"),
         Position => Windows.Centered_Window_Position,
         Size     => Window_Size);

      delay 3.0;

      Windows.Finalize (Game_Window);
      SDL.Quit;
   end if;
end Ada_Game;
```

Build and run, this should make the screen flicker and will probably copy the existing
screen contents to the new window, then it will close.

```console
$ alr run
```

## Adding a game loop

We want the game to do more than just show a window for a bit and then exit. So,
we need a game loop, you can use a while loop:

```ada
Main : while not Finished loop
   --  Do things in here.
end loop Main;
```

Or an infinite loop:

```ada
Main : loop
   --  Do things in here.

   exit Main when Finished;
end loop Main;
```

Pick one of these and replace the delay statement with it, don't forget to add
the condition variable.

## Exiting the game

We don't want the game to execute forever, so we need a nice clean way to exit
the game. A real game would have a menu displayed which would provide an exit
option. For this example, we'll exit the game with the Escape key and the close
window icon.

**N.B:** The `SDL.Events.*` package hierarchy is a lot of a mess, they will
be changed in future. I originally wanted to have separate packages for each kind
of event, mouse, keyboard, etc. But due to Ada's view of packages, I couldn't
the final Event record which combines them all the way I wanted.

We need an `Events` object we can pull the current event into so we can
examine it:

```ada
Event : SDL.Events.Events.Events;
```

As we have used this package, we need a `with` clause:

```ada
with SDL.Events.Events;
```

Then we can add the even polling loop, replace the "Do things in here." comment:

```ada
      Process_Input : while SDL.Events.Events.Poll (Event) loop
         case Event.Common.Event_Type is
            when SDL.Events.Quit =>
               Finished := True;

            when SDL.Events.Keyboards.Key_Up =>
               if Event.Keyboard.Key_Sym.Key_Code = SDL.Events.Keyboards.Code_Escape then
                  Finished := True;
               end if;

            when others =>
               null;
         end case;
      end loop Process_Input;
```

This won't compile because the `Key_Code` operators aren't visible, add the
following line after the previous package `renames` statement:

```ada
   use type SDL.Events.Keyboards.Key_Codes;
```

Now run and test that the escape key and the close window button work as expected.

## Switching between full screen and windowed modes

You will want to be able to switch between a windowed mode and full screen whilst
debugging, so allow an easy way to switch.

A real game would have a menu system which provided a way to select a screen
resolution and whether it's windowed or not.

Change the following:

```ada
      Position => Windows.Centered_Window_Position,
      Size     => Window_Size);
```

To:

```ada
      Position => Windows.Centered_Window_Position,
      Size     => Window_Size,
      Flags    => Mode or Windows.OpenGL);
```

The variable `Mode` needs to be created:

```ada
   Mode        : Windows.Window_Flags := Windows.Windowed;
```

Now we can extend the event loop `if` statement with the following which
will change the window mode:

```ada
               elsif Event.Keyboard.Key_Sym.Key_Code = SDL.Events.Keyboards.Code_F then
                  if Mode = Windows.Windowed then
                     Mode := Windows.Full_Screen_Desktop;
                  elsif Mode = Windows.Full_Screen_Desktop then
                     Mode := Windows.Windowed;
                  end if;

                  Windows.Set_Mode (Game_Window, Mode);
```

## Creating a blank canvas

In SDL2, the way to draw to the screen is to draw to an off-screen texture and then
present that texture to the renderer. This is basically, off screen rendering.

With the correct packages:

```ada
with SDL.Video.Pixel_Formats;
with SDL.Video.Renderers.Makers;
with SDL.Video.Textures.Makers;
```

Add more renames:

```ada
   package Pixel_Formats renames Video.Pixel_Formats;
   package Renderers renames Video.Renderers.Makers;
   package Textures renames Video.Textures.Makers;
```

We need to add both a renderer and a texture to render:

```ada
   Renderer : Renderers.Renderer;
   Texture  : Textures.Texture;
```

After the window creation function, add the following:

```ada
   Renderers.Makers.Create (Window => Window,
                            Rend   => Renderer,
                            Flags  => Accelerated);
```

The Flags just specify that we want a hardware accelerated renderer. Under the
covers, SDL2 will create either an OpenGL, Metal, Vulkan or DX accelerated
renderer.

Following this, we need to create a texture which will be our game world, or
screen drawing surface. This will be used to implement double buffering, as you'll
normally draw to the texture, then present it to the renderer which will then
draw the texture onto the window.

```ada
   Textures.Makers.Create (Tex      => Texture,
                           Renderer => Renderer,
                           Format   => Pixel_Formats.Pixel_Format_ARGB_8888,
                           Kind     => Textures.Streaming,
                           Size     => Window_Size);
```

Specifying the pixel format as ARGB 8888, just means your colour palette can use
the full 24-bit colour range with 8-bits of transparency (alpha channel).

You will normally want the texture size to be the same size as the window. The
main reason for having a smaller texture is for pixel art style games, where you
would specify a a much smaller texture which is then scaled by SDL2 when presented
to the renderer to the ful screen. Adam C. Younis has a good explanation of which
resolutions to use for this in his [canvas sizes](https://youtu.be/upEGBGCiWEw?si=v8aqmpol-CkRfiIl&t=1020) video.

Now to present the thing to the screen. After the event while loop, add the
following lines:

```ada
         Renderers.Clear (Renderer);
         Renderers.Present (Renderer);
```

When you run this game, you will now have a glorious black window, yay! Pressing F will
toggle between a black fullscreen and a black window.

![Ada SDL2 Window](../../../static/img/tutorials/start_sdl2/ada_sdl2.png)

## Drawing to the screen

Now we want to draw something, anything, to the screen, just to prove our game
is working fine. So, we need to define a triangle to draw, add the following:

```ada
   Triangle    : Renderers.Vertex_Arrays (1 .. 3) := (
      1 => Renderers.Vertices'(Position           => (X => 10.5, Y => 10.5),
                               Colour             => (Red | Alpha => 255, others => 0),
                               Texture_Coordinate => <>),
      2 => Renderers.Vertices'(Position           => (X => 50.5, Y => 10.5),
                               Colour             => (Red | Alpha => 255, others => 0),
                               Texture_Coordinate => <>),
      3 => Renderers.Vertices'(Position           => (X => 10.5, Y => 50.5),
                               Colour             => (Red | Alpha => 255, others => 0),
                               Texture_Coordinate => <>));
```

Before the `Clear` function call, add the following line:

```ada
         Renderers.Set_Draw_Colour (Renderer, (others => 255));
```

This will set the screen colour to white.

Then to actually draw, add the following line before the `Present` function
call:

```ada
         Renderers.Render_Geometry (Renderer, Triangle);
```

And there we have a small triangle in the top left corner of the window.

![Ada SDL2 Window with Triangle](../../../static/img/tutorials/start_sdl2/ada_sdl2-triangle.png)

## Adding an FPS counter

It's useful to know how fast the game is running, game loops can slow things down
and that isn't desirable.

There are two ways to add text:

1. Use SDL TTF library.
2. Use a texture.

I will be using the first method here.

Start by importing the TTF library, you don't need to link anything as it's all
included.

```ada
with SDL.TTFs.Makers;
```

Then add a rename to make things a bit easier to read:

```ada
   package TTFs renames SDL.TTFs;
```

Along with the SDL initialisation, we may as well Initialise the TTF library:

```ada
   if SDL.Initialise and then TTFs.Initialise then
```

and then near the end clean up:

```ada
// highlight-next-line
      TTFs.Quit;
      Windows.Finalize (Game_Window);
```

We need a `Fonts` object to put our font into:

```ada
// highlight-next-line
   Font        : TTFs.Fonts;
begin
```

The font needs to be loaded in from disk, for this we will pass it as a command
line argument, that way you can use whatever TTF font you like that is available
to you as they can vary per installation. First bring in the command line package:

```ada
with Ada.Command_Line;
```

Then create the font object:

```ada
// highlight-start
      TTFs.Makers.Create (Font,
                          File_Name  => Ada.Command_Line.Argument (1),
                          Point_Size => 48);
// highlight-end

      Main : loop
```

When we draw the text, it has to be drawn to a Surface and then the surface needs
to be rendered to the window texture. We'll need the `Surface` package:

```ada
with SDL.Video.Surfaces;
```

and a renaming:

```ada
   package Surfaces renames Video.Surfaces;
```

Then we can render the text:

```ada
         Renderers.Clear (Renderer);
// highlight-start


         FPS : declare
            Text_Surface : Surfaces.Surface := TTFs.Render_Solid
              (Font,
               Text   => "FPS: 9999",
               Colour => (Alpha => 255, others => 0));
            Text_Texture : Textures.Texture;

            use type SDL.Dimension;
         begin
            Textures.Makers.Create (Text_Texture, Renderer, Text_Surface);
            Renderers.Copy (Renderer, Text_Texture, To => (X      => Window_Size.Width - 200,
                                                           Y      => 0,
                                                           Width  => 200,
                                                           Height => 48));
         end FPS;

// highlight-end
         Renderers.Render_Geometry (Renderer, Triangle);
```

If you change the X co-ordinate of the FPS counter texture, you'll see that it
is rendered under the triangle, make sure you render this last by moving the
`Render_Geometry` procedure call to above the FPS rendering.

You might want to use the `Render_Shaded` function instead if you have trouble
seeing the FPS counter.

Now, add a constant to the FPS block:

```ada
         FPS : declare
// highlight-next-line
            FPS_Text     : constant String := "FPS: ";
```

and then use this to construct the text rendered onto the surface:

```ada
            Text_Surface : Surfaces.Surface := TTFs.Render_Solid
              (Font,
               Text   => FPS_Text & "9999",
               Colour => (Alpha => 255, others => 0));
```

We want to make sure the FPS counter gets a number instead of "9999."
